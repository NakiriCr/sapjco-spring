apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'

apply from: 'artifact.gradle'

group 'cn.yanzx-dev.gitlab'
version artifactVersion

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots//" }
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-parameters"
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
}

configurations {
    all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    compile fileTree (dir:'libs',include:['*.jar'])

    compile group: 'cn.yanzx-dev.gitlab', name: 'com.sap.conn.jco.sapjco', version: '3.0.14-SNAPSHOT'
    compile group: 'com.google.guava', name: 'guava', version: '23.0'
    compile group: 'org.springframework', name: 'spring-context', version: '4.3.2.RELEASE'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.4'
}


task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'cn.yanzx-dev.gitlab'
            artifactId = 'sapjco-spring'
            version = '3.0.5-SNAPSHOT'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                // artifact name
                name = 'Sapjco Spring'
                // artifact description
                description = 'SAP Java Connector integration with Spring'
                // artifact homepage
                url = 'https://gitlab.yanzx-dev.cn/VirtualCry/sapjco-spring'
                // licenses
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                // developers info
                developers {
                    developer {
                        name = 'VirtualCry'
                        email = '172519267@qq.com'
                    }
                }
                // scm
                scm {
                    url = 'https://gitlab.yanzx-dev.cn/VirtualCry/sapjco-spring'
                    connection = 'scm:git:https://gitlab.yanzx-dev.cn/VirtualCry/sapjco-spring.git'
                    developerConnection = 'scm:git:https://gitlab.yanzx-dev.cn/VirtualCry/sapjco-spring.git'
                }
            }
        }
    }
    // repositories to publish
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

jar {
    manifest {
        attributes 'Built-By': 'VirtualCry'
        attributes 'Created-By': 'Gradle-' + gradle.getGradleVersion()
        attributes 'Build-Jdk': java.getTargetCompatibility()
    }
}
