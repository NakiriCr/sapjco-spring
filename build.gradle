apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group 'cn.yanzx-dev.gitlab'
version '3.0.0.BUILD-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "http://maven.mit.edu/nexus/content/repositories/public/" }
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-parameters"
}

sourceSets {

    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
}

configurations {
    all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    compileOnly fileTree (dir:'libs',include:['*.jar'])

    compileOnly group: 'com.sap.jco', name: 'sapjco', version: '3.0.14'
    compileOnly group: 'org.springframework', name: 'spring-context', version: '4.3.2.RELEASE'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.4'
}


task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'cn.yanzx-dev.gitlab'
            artifactId = 'jco-spring'
            version = '3.0.0.BUILD-SNAPSHOT'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                // artifact name
                name = 'jco-spring'
                // artifact description
                description = 'jco with spring'
                // artifact homepage
                url = 'https://gitlab.yanzx-dev.cn/VirtualCry/jco-spring'
                // licenses
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                // developers info
                developers {
                    developer {
                        name = 'VirtualCry'
                        email = '172519267@qq.com'
                    }
                }
                // scm
                scm {
                    url = 'https://gitlab.yanzx-dev.cn/VirtualCry/jco-spring'
                    connection = 'scm:git:https://gitlab.yanzx-dev.cn/VirtualCry/jco-spring.git'
                    developerConnection = 'scm:git:https://gitlab.yanzx-dev.cn/VirtualCry/jco-spring.git'
                }
            }
        }
    }
    // repositories to publish
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
